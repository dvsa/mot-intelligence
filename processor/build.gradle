group 'uk.gov.dvsa.mot'
version '0.1'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "io.spring.dependency-management"

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

mainClassName = 'uk.gov.dvsa.moti.processor.ProcessorApplication'

jar {
    archiveName "${baseName}.${extension}"
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

sourceCompatibility = 1.8


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RC2"
    }
}

sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
        resources.srcDir 'src/test/unit/resources'
    }
    integrationTest {
        java.srcDir 'src/test/integration/java'
        resources.srcDir 'src/test/integration/resources'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://nexus-prd.motdev.org.uk:8080/nexus/content/repositories/mot/"
    }
    maven {
        url "http://nexus-prd.motdev.org.uk:8080/nexus/content/repositories/mot-snapshots/"
    }
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.10.77'
    }
}

dependencies {
    testCompile (
            'org.mockito:mockito-core:1.10.19',
            'junit:junit:4.12'
    )
    compile (
            'org.apache.commons:commons-collections4:4.1',
            'uk.gov.dvsa.mot:common-libraries:1.0.0',
            'org.apache.commons:commons-compress:1.3',
            'com.amazonaws:aws-java-sdk-s3',
            'net.logstash.logback:logstash-logback-encoder:4.8',
            'com.google.inject:guice:4.1.0',
            project(':core'),
    )
    integrationTestCompile (
            sourceSets.main.output,
            configurations.testCompile,
    )
}

shadowJar {
    baseName = 'moti-processor'
    archiveName = "${baseName}-all.${extension}"
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

run {
    args 'config.yml'
}

apply plugin: 'nebula.rpm'
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.0'
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    group = 'verification'
    description = 'Runs the integration tests.'
    systemProperty "configurationFilePath", System.getProperty("configurationFilePath")!=null ? System.getProperty("configurationFilePath") :  "config.yml"
}

// BuildNumber has to be set by -PBuildNumber or ORG_GRADLE_PROJECT_BuildNumber env var
task BuildRpm(type: Rpm) {
    packageName 'moti-webapp-processor'
    version = project.version
    release = project.hasProperty('BuildNumber') ? BuildNumber : 'dev'
    packageDescription = project.hasProperty('Description') ? Description: 'none'
    user 'moti-webapp'
    permissionGroup 'moti-webapp'
    license 'Proprietary'
    url 'https://gitlab.motdev.org.uk/moti/mot-intelligence'
    arch X86_64
    os LINUX
    type BINARY

    preInstall file('../scripts/rpm/preInstall')
    postInstall file('../scripts/rpm/postInstallProcessor')
    postUninstall file('../scripts/rpm/postUninstall')

    from('src/dist/config/mot-intelligence.yml') {
      into '/etc/dvsa/moti-webapp/'
      fileMode 0750
      user 'root'
      permissionGroup 'moti-webapp'
      fileType CONFIG | NOREPLACE
    }

    into '/opt/dvsa/moti-webapp'
    from ('build/libs/moti-processor-all.jar') {
      fileMode 0750
      user 'root'
      permissionGroup 'moti-webapp'
      into 'lib'
    }

    requires ('java-1.8.0-openjdk', '1.8.0.65', GREATER | EQUAL)
    link ('/opt/dvsa/moti-webapp/log', '/var/log/dvsa/moti-webapp')
    link ('/opt/dvsa/moti-webapp/conf', '/etc/dvsa/moti-webapp')
}

