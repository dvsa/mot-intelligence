group 'uk.gov.dvsa.mot'
version '0.1'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-bin.zip"
}

mainClassName = 'uk.gov.dvsa.moti.processing.ProcessorApplication'

jar {
    archiveName "${baseName}.${extension}"
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

sourceCompatibility = 1.8


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

sourceSets {
    test {
        java.srcDir 'src/test/unit/java'
        resources.srcDir 'src/test/unit/resources'
    }
    integrationTest {
        java.srcDir 'src/test/integration/java'
        resources.srcDir 'src/test/integration/resources'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://nexus-prd.motdev.org.uk:8080/nexus/content/repositories/mot/"
    }
    maven {
        url "http://nexus-prd.motdev.org.uk:8080/nexus/content/repositories/mot-snapshots/"
    }
}

dependencies {
    testCompile (
            'org.mockito:mockito-core:1.10.19',
            'junit:junit:4.12'
    )
    compile (
            'org.apache.commons:commons-collections4:4.1',
            'uk.gov.dvsa.mot:common-libraries:1.0.0',
            'org.apache.commons:commons-compress:1.3',
            'com.amazonaws:aws-java-sdk-s3:1.11.90',
            'net.logstash.logback:logstash-logback-encoder:4.8',
            'com.google.inject:guice:4.1.0',
            project(':core'),
    )
    integrationTestCompile (
            sourceSets.main.output,
            configurations.testCompile,
    )
}

shadowJar {
    baseName = 'moti-processor'
    archiveName = "${baseName}-all.${extension}"
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

run {
    args 'config.yml'
}

apply plugin: 'nebula.rpm'
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.0'
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    group = 'verification'
    description = 'Runs the integration tests.'
    systemProperty "configurationFilePath", System.getProperty("configurationFilePath")!=null ? System.getProperty("configurationFilePath") :  "config.yml"
}

// BuildNumber has to be set by -PBuildNumber or ORG_GRADLE_PROJECT_BuildNumber env var
task BuildRpm(type: Rpm) {
    packageName 'moti-processor'
    version = project.version
    release = project.hasProperty('BuildNumber') ? BuildNumber : 'dev'
    packageDescription = project.hasProperty('DescriptionRPM') ? DescriptionRPM: 'none'
    user 'moti-processor'
    permissionGroup 'moti-processor'
    license 'Proprietary'
    url 'https://gitlab.motdev.org.uk/moti/mot-intelligence'
    arch X86_64
    os LINUX
    type BINARY

    preInstall file('scripts/rpm/preInstall')
    postInstall file('scripts/rpm/postInstall')
    postUninstall file('scripts/rpm/postUninstall')

    from('config.yml.dist') {
      into '/etc/dvsa/moti-processor/conf/'
      rename { String fileName ->
          fileName.replace('config.yml.dist', 'config.yml')
      }
      fileMode 0750
      user 'root'
      permissionGroup 'moti-processor'
      fileType CONFIG | NOREPLACE
    }

    into '/opt/dvsa/moti-processor'
    from ('build/libs/moti-processor-all.jar') {
      fileMode 0750
      user 'root'
      permissionGroup 'moti-processor'
      into 'lib'
    }

    requires ('java-1.8.0-openjdk', '1.8.0.65', GREATER | EQUAL)
    link ('/opt/dvsa/moti-processor/log', '/var/log/dvsa/moti-processor')
    link ('/opt/dvsa/moti-processor/conf', '/etc/dvsa/moti-processor')
}

